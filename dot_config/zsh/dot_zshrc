# vim: set ft=zsh:

# This line sources shell-independent environment variables. While there is
# considerable debate about whether such variables should be sourced in zprofile
# or zshrc, sourcing them in zshrc is preferred for broader compatibility across
# linux and macOS systems. This ensures that the environment variables are
# consistently available across all interactive shells, regardless of how
# they're initiated. Given the efficiency of modern computers, the performance
# impact of sourcing these variables for every new interactive shell is
# negligible.
[ -f ~/.config/shell/env ] && source ~/.config/shell/env

# Initialize Powerlevel10k instant prompt. Keep this block near the top of zshrc
# to ensure the prompt appears immediately when a new shell session starts.
if [[ -r "$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh" \
        && ( -n "$TMUX" || -n "$SSH_TTY" ) ]]; then
    source "$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Hostname up to the first '.'
export SHORT_HOST=${SHORT_HOST:-${(%):-%m}}

# Define new compdump location. The zsh directory must be created manually
export ZSH_COMPDUMP="$XDG_CACHE_HOME/zsh/zcompdump-${SHORT_HOST}-${ZSH_VERSION}"

# When pressing ESC, use a minimal timeout of 1 ms
export KEYTIMEOUT=1

export HISTFILE="$XDG_STATE_HOME/zsh/history"
export ZSH_SESSIONS_DIR="$XDG_STATE_HOME/zsh/sessions"

# Remove duplicates in paths
typeset -gU cdpath fpath mailpath path

# Show no duplicates in history
setopt HIST_FIND_NO_DUPS

# Oh-my-zsh settings. Must be defined before oh-my-zsh is loaded
HYPHEN_INSENSITIVE='true' # Treat '-' and '_' the same for completion purposes
DISABLE_AUTO_TITLE='true' # Disable auto-setting terminal title
COMPLETION_WAITING_DOTS='true' # Show red dots while waiting for completion
ZSH_DISABLE_COMPFIX='true'
DISABLE_AUTO_UPDATE='true'

# Update the highlight color of ZVM. Must be defined before ZVM is loaded
function zvm_config() {
    ZVM_VI_HIGHLIGHT_BACKGROUND=8
    ZVM_VI_HIGHLIGHT_FOREGROUND=0
}

# ZVM overwrites the default keybindings of some plugins. To ensure that certain
# keybindings take precedence, we need to define them after ZVM is loaded.
function zvm_after_init() {
    # Setup keybindings for fzf
    if command -v fzf >/dev/null 2>&1; then
        source <(fzf --zsh)
    fi

    # Remap CTRL-k to CTRL-x due to VIM-TMUX Navigator conflict
    bindkey '^X' kill-line
}

# Add shell functions to fpath and autoload them
fpath=( $XDG_CONFIG_HOME/shell/functions "${fpath[@]}" )
autoload -Uz $fpath[1]/*(.:t)

# Add all directories containing completions to fpath and autoload the
# completions. Must be done before oh-my-zsh calls compinit
if [ -d $XDG_DATA_HOME/zsh/zsh-completions ]; then
    fpath=( $XDG_DATA_HOME/zsh/zsh-completions "${fpath[@]}" )
    autoload -Uz $fpath[1]/*(.:t)
fi
if type brew &> /dev/null; then
    fpath=( "$(brew --prefix)/share/zsh/site-functions" "${fpath[@]}" )
    autoload -Uz $fpath[1]/*(:t)
fi
fpath=( $XDG_DATA_HOME/zsh/custom-completions "${fpath[@]}" )
autoload -Uz $fpath[1]/*(.:t)
# Delete compiled completions and recompile them with:
# 'rm -f $ZSH_COMPDUMP; compinit -d "$ZSH_COMPDUMP"'

[[ -s "$BASE16_SHELL_PATH/profile_helper.sh" ]] && \
    source "$BASE16_SHELL_PATH/profile_helper.sh"

[[ -s "$XDG_DATA_HOME/zsh/powerlevel10k/powerlevel10k.zsh-theme" ]] \
    && source "$XDG_DATA_HOME/zsh/powerlevel10k/powerlevel10k.zsh-theme"

[[ -s "$XDG_DATA_HOME/zsh/zsh-autosuggestions/zsh-autosuggestions.zsh" ]] \
    && source "$XDG_DATA_HOME/zsh/zsh-autosuggestions/zsh-autosuggestions.zsh"

[[ -s "$XDG_DATA_HOME/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" ]] \
    && source "$XDG_DATA_HOME/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

# Get rid of OMZ. Read all files in oh-my-zsh/lib and extract important cmds
[[ -s "$XDG_DATA_HOME/zsh/oh-my-zsh/oh-my-zsh.sh" ]] \
    && source "$XDG_DATA_HOME/zsh/oh-my-zsh/oh-my-zsh.sh"

# Disable lazy loading. See: https://github.com/jeffreytse/zsh-vi-mode/issues/258
ZVM_INIT_MODE=sourcing

[[ -s "$XDG_DATA_HOME/zsh/zsh-vi-mode/zsh-vi-mode.zsh" ]] \
    && source "$XDG_DATA_HOME/zsh/zsh-vi-mode/zsh-vi-mode.zsh"


# zsh wants you to verify history expansion. I want to type 'sudo !!' without
# further verification
unsetopt HIST_VERIFY

# Source the 'powerlevel10k' config
[ -f ${ZDOTDIR}/p10k.zsh ] && source ${ZDOTDIR}/p10k.zsh

# With 'base16-shell', the default 'zsh-autosuggestions' color (color8) closely
# matches the terminal foreground color, making it hard to distinguish
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=19'

# FZF should use 'fd' instead of the default 'find'
if command -v fd >/dev/null 2>&1; then
    # Run by nvim **<tab>
    _fzf_compgen_path() {
        fd --type f --hidden --follow "$1"
    }
    # Run by cd **<tab>
    _fzf_compgen_dir() {
        fd --type d --hidden --follow "$1"
    }
    # Used by C-t
    export FZF_CTRL_T_COMMAND="fd --type f --hidden --follow"
fi

# The plugin 'syntax-highlighting' adds undercores to paths. Let's turn that off
(( ${+ZSH_HIGHLIGHT_STYLES}  )) || typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[path]=none
ZSH_HIGHLIGHT_STYLES[path_prefix]=none
ZSH_HIGHLIGHT_STYLES[autodirectory]=none
ZSH_HIGHLIGHT_STYLES[precommand]=fg=green

# Source lf icons file
[ -f ~/.config/lf/lficons ] && source ~/.config/lf/lficons

# Source my aliases file
[ -f "$XDG_CONFIG_HOME/shell/aliases" ] && source "$XDG_CONFIG_HOME/shell/aliases"

# Initialize conda
if [ -f "$CONDA_HOME/etc/profile.d/conda.sh" ]; then
    source "$CONDA_HOME/etc/profile.d/conda.sh"
fi

# Autostart tmux
if (( $+commands[tmux] )); then
    autostart-tmux
fi

# Return with exit code 0
true
